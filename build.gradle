/*
 * Copyright (c) 2021 SailPoint Technologies, Inc.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

version = '0.3.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/releases/"
    }
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {

    // Nimbus JOSE JWT
    // https://mvnrepository.com/artifact/com.nimbusds/nimbus-jose-jwt
    api group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.30.1'
    implementation group: 'com.sailpoint', name: 'ietf-subject-identifiers-model', version: '0.2.0-SNAPSHOT'
    //implementation files("../ietf-subject-identifiers-model/build/libs/ietf-subject-identifiers-model-0.2.0-SNAPSHOT.jar")
    // Use JUnit test framework
    testImplementation 'junit:junit:4.13.2'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

java {
    modularity.inferModulePath = true
    withJavadocJar()
    withSourcesJar()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        model(MavenPublication) {
            from components.java
            pom {
                name = 'openid-sse-model'
                description = 'OpenID Shared Signals and Events models'
                url = 'http://github.com/sailpoint-oss/openid-sse-model'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'adam-hampton-sp'
                        name = 'Adam Hampton'
                        email = 'adam.hampton@sailpoint.com'
                    }
                    developer {
                        id = 'matt-domsch-sp'
                        name = 'Matt Domsch'
                        email = 'matt.domsch@sailpoint.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/sailpoint-oss/openid-sse-model.git'
                    developerConnection = 'scm:git:ssh://github.com/sailpoint-oss/openid-sse-model.git'
                    url = 'https://github.com/sailpoint-oss/openid-sse-model/'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'myRepo'
            url = "file://${buildDir}/repo"
        }
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.model
}

group = 'com.sailpoint'
sourceCompatibility = 9;
targetCompatibility = 9;
